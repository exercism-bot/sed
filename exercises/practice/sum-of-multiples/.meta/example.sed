v 4.9

s/\s+//g
s/\}$//
s/^\{"property":"sum",//
s/,"limit":/\n\cL/
s/[][]//g
s/"factors":/\cF,/

/\cF[0,]+\n/ c 0

s/,0\b//g

:n2h
s/[0-9]/@&/g
s/0//g
s/1/#/g
s/2/##/g
s/3/###/g
s/4/####/g
s/5/#####/g
s/6/######/g
s/7/#######/g
s/8/########/g
s/9/#########/g
:tens; s/#@/@##########/g; t tens
s/@//g

s/$/\n\cS/

:fact
s/,(#+)\n/\n\cF\1*\n/
:inc
s/\*/*#/
s/\cF(#+\*#+)\n/&\cM@\1\n/
:multiply
s/@(#+)\*(#+)#/\1@\1*\2/
s/(\cM[#@]+)\*#\n/\1\n/
/\cM[@#]+\*/ b multiply
s/@//g

/\cM(#+)\n(\cL\1#+\n\cS)/ {
    s//\2\1+/
    b inc
}

s/\cF[#*]+\n\cM#+\n//

/\cF,/ b fact

/\cS$/ c 0

s/.*\cS/+/
s/\+/\n/g

s/#+/<&>/g
:undup
s/\n(<#+>)(.*)\n\1/\n\1\2/; t undup
s/[\n<>]//g

:h2n
s/##########/@/g
s/@([0-9]*)([^0-9#@]|$)/@0\1\2/g
s/#########/9/g
s/########/8/g
s/#######/7/g
s/######/6/g
s/#####/5/g
s/####/4/g
s/###/3/g
s/##/2/g
s/#/1/g
s/@/#/g
t h2n
